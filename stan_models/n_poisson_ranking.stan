data {
  int N;
  int T;
  array[N] int team_i;
  array[N] int team_j;
  array[N] int y_i;
  array[N] int y_j;
  vector[N] game_weight;
  vector[T] prior_strength;
}

parameters {
  vector[T] attack_raw_std; // standard normal inputs
  vector[T] defense_raw_std;
  real eta;
  real<lower=0.01> sigma_att;
  real<lower=0.01> sigma_def;
  
  real beta_prior;
}

transformed parameters {
  vector[T] attack_raw;
  vector[T] defense_raw;
  attack_raw = (prior_strength * beta_prior) + (attack_raw_std * sigma_att);
  defense_raw = defense_raw_std * sigma_def;
  vector[T] attack = attack_raw - mean(attack_raw);
  vector[T] defense = defense_raw - mean(defense_raw);
}

model {
  // 1. Priors for the standardized variables
  attack_raw_std ~ normal(0, 1);
  defense_raw_std ~ normal(0, 1);

  // 2. Hyperparameters
  eta ~ normal(0, 1);
  beta_prior ~ normal(0, 1);
  sigma_att ~ cauchy(0, 2.5); 
  sigma_def ~ cauchy(0, 2.5);

  for (n in 1:N) {
    target += game_weight[n] * (
      poisson_log_lpmf(
        y_i[n] | attack[team_i[n]] - defense[team_j[n]] + eta
      )
      + poisson_log_lpmf(
        y_j[n] | attack[team_j[n]] - defense[team_i[n]] + eta
      )
    );
  }
}
